apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "ar.rulosoft.mimanganu"
        versionCode 121
        versionName "1.121"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 15
    }
    signingConfigs {
        release {
            storeFile file("../mimanganu.jks")
            storePassword 'mimanganu'
            keyAlias 'mimanganu'
            keyPassword 'mimanganu'
        }
        debug {
            storeFile file("../mimanganu.jks")
            storePassword 'mimanganu'
            keyAlias 'mimanganu'
            keyPassword 'mimanganu'
        }
    }
    buildTypes {
        release {
            lintOptions {
                disable 'MissingTranslation'
                disable 'NewApi' //Avoid problems in Android Studio 2.4 preview 6
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.debug
	    lintOptions {
          	abortOnError false
     	    }
	    testCoverageEnabled true
        }
    }
    compileOptions.encoding = 'UTF-8'
    flavorDimensions "builds"
    productFlavors {
        fdroid {
            signingConfig signingConfigs.release
            dimension "builds"
        }
        github {
            signingConfig signingConfigs.release
            //removed to maintain updates as now are
            versionNameSuffix '-github'
            dimension "builds"
        }
    }
}

android.sourceSets {
    test {
        java.srcDirs += "$projectDir/src/testShare"
    }

    androidTest {
        java.srcDirs += "$projectDir/src/testShare"
    }
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:25.3.1'
}

dependencies {
    final ANDROID_SUPPORT = '28.0.0'
    implementation "com.android.support:support-v4:$ANDROID_SUPPORT"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:design:$ANDROID_SUPPORT"
    implementation "com.android.support:appcompat-v7:$ANDROID_SUPPORT"
    implementation "com.android.support:recyclerview-v7:$ANDROID_SUPPORT"
    implementation "com.android.support:preference-v7:$ANDROID_SUPPORT"
    implementation 'com.github.sevar83:indeterminate-checkbox:1.0.5@aar'
    implementation group: 'org.brotli', name: 'dec', version: '0.1.1'

    //implementation group: 'com.github.junrar', name: 'junrar', version: '4.0.0'
    api project(':imageviewtouchlibrary')
    implementation 'rapid.decoder:library:0.3.0'
    implementation 'rapid.decoder:jpeg-decoder:0.3.0'
    implementation 'rapid.decoder:png-decoder:0.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1' //on 3.10.0 are deprecated some ciphers
    implementation 'com.squareup.okio:okio:1.15.0'
    implementation 'com.squareup.duktape:duktape-android:1.1.0'/* v 1.2.0 need 4+mb extra*/
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    implementation 'com.evernote:android-job:1.2.5'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    githubImplementation 'com.google.android.gms:play-services-base:11.0.0'
    //implementation 'ch.acra:acra:4.9.2'
    fdroidApi project(':fakegps')
    // for device-based testing
    androidTestImplementation "com.android.support:support-annotations:$ANDROID_SUPPORT"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'junit:junit:4.12'
    // for host-based testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.0-beta-1'
    testImplementation 'androidx.test:core:1.2.0'

    android {
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }
}


apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.3"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoUnitTestReport(type: JacocoReport, dependsOn: ['testGithubDebugUnitTest']) {

    reports {
        xml.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/javac/githubDebug/compileGithubDebugJavaWithJavac/classes", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            'jacoco/testGithubDebugUnitTest.exec'
    ])
}

